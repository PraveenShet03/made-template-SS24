pipeline GoodsPipeline {
    GoodsExtractor 
        -> GoodsTextFileInterpreter
        -> GoodsTextRangeSelector
        -> GoodsCSVInterpreter
        -> GoodsHeaderSequenceWriter_AtoE
        -> GoodsHeaderSequenceWriter_ATtoAU
        -> GoodsTableInterpreter
        -> GoodsLoader;
    
    block GoodsExtractor oftype HttpExtractor {
        url: "https://www-genesis.destatis.de/genesis/downloads/00/tables/46131-0014_00.csv";
    }

    block GoodsTextFileInterpreter oftype TextFileInterpreter { encoding: "latin3"; }

    block GoodsCSVInterpreter oftype CSVInterpreter {
        enclosing: '';
        delimiter: ";";
    }

    block GoodsTextRangeSelector oftype TextRangeSelector {
        lineFrom: 8;
        lineTo: 41336;
    }
    
    block GoodsHeaderSequenceWriter_AtoE oftype CellWriter {
        at: range A1:E1;
        write: ["year", "month", "goods_id", "goods_name", "goods_source"];
        
    }

    block GoodsHeaderSequenceWriter_ATtoAU oftype CellWriter{
        at: range AT1:AU1;
        write: ["abroad","total"];
    }

    valuetype PositiveInteger oftype integer{
        constraints: [ RangePositiveInteger];
    }

    constraint RangePositiveInteger oftype RangeConstraint{
        lowerBound: 0;
        lowerBoundInclusive: false;
    }

    valuetype ValidGermanMonth oftype text{
        constraints: [ListGermanMonth];
    }

    constraint ListGermanMonth oftype AllowlistConstraint {
        allowlist: ["Januar","Februar","MÃ¤rz","April","Mai","Juni","Juli","August","September","Oktober","November","Dezember"];
    }

    valuetype ValidGoodsId oftype text{
        constraints: [RegexGoodsId];
    }

    constraint RegexGoodsId oftype RegexConstraint{
        regex: /^NST7-[0-9A-Z]{3}$/;
    }


    block GoodsTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "year" oftype PositiveInteger,
            "month" oftype ValidGermanMonth,
            "goods_id" oftype ValidGoodsId,
            "goods_name" oftype text,
            "goods_source" oftype text,
            "abroad" oftype PositiveInteger,
            "total" oftype PositiveInteger,
        ];
    }

    block GoodsLoader oftype SQLiteLoader {
        table: "goods";
        file: "goodsTransportedByTrain.sqlite";
    }
}
