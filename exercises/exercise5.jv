pipeline TransportsPipeline {
    TransportsExtractor
        ->TransportsArchiveInterpreter
        ->TransportsTextFilePicker
        ->TransportsTextInterpreter
        ->TransportsCSVInterpreter
        ->TransportsTableInterpreter
        ->TransportsLoader;
    
    block TransportsExtractor oftype HttpExtractor {
        url: 'https://gtfs.rhoenenergie-bus.de/GTFS.zip';
    }

    block TransportsArchiveInterpreter oftype ArchiveInterpreter {
        archiveType: 'zip';
    }

    block TransportsTextFilePicker oftype FilePicker { 
        path: '/stops.txt';
    }

    block TransportsTextInterpreter oftype TextFileInterpreter {}
    
    block TransportsCSVInterpreter oftype CSVInterpreter {
        delimiter: ",";
        enclosing: '"';
    }    

    block TransportsTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            'stop_id' oftype integer,
            'stop_name' oftype nameIncludesUmlauts,
            'stop_lat' oftype coordinate,
            'stop_lon' oftype coordinate,
            'zone_id' oftype stopZoneId,
        ];
    }

    valuetype nameIncludesUmlauts oftype text {
        constraints: [RegexNameIncludesUmlauts];
    }

    constraint RegexNameIncludesUmlauts oftype RegexConstraint {
        regex: /[A-Za-zäöüÄÖÜß]+/;
    }

    valuetype stopZoneId oftype integer {
        constraints: [validZoneIdConstraint];
    }

    constraint validZoneIdConstraint on integer:
        value >= 0 and value == 1645;

    valuetype coordinate oftype decimal {
        constraints: [validCoordinate];
    }

    constraint validCoordinate oftype RangeConstraint {
        lowerBound: -90;
        upperBound: 90;
    }

    block TransportsLoader oftype SQLiteLoader {
        table: 'stops';
        file: './gtfs.sqlite';
    }
}